////////////////////////////////////////////////////////////////////////////////
//
// Máquina de Turing para reconocer hileras de la forma X#X
//
////////////////////////////////////////////////////////////////////////////////

// Autor: Alvaro de la Ossa Osegueda
// Fecha: 7 de junio de 2020

// Este programa fue preparado para ser ejecutado en el simulador disponible en
// turingmachinesimulator.com, (Copyleft) Martín Ugarte, 2015.

// Problema: Reconocer hileras de la forma ...

// Mátuina: MTTarea2
// Estados: q0,q1,q2,q3,q4,q5,q6,q7,q8,q9
// Alfabeto de entrada: 0,1,#
// Alfabeto de la cinta: 0,1,x,#
// Estado inicial: q0
// Estados de aceptación y de rechazo: q8,q9

////////////////////////////////////////////////////////////////////////////////

name: MTTarea2
init: e0
accept: e8

///// En estado inicial e0

// q0,0,q1,x,R-q0,1,q2,x,R-q0,#,q7,#,R
e0, 0
e1, x, >
e0, 1
e2, x, >
e0, #
e7, #, >

// q1,0,q1,0,R-q1,1,q1,1,R-q1,#,q3,#,R
e1, 0
e1, 0, >
e1, 1
e1, 1, >
e1, #
e3, #, >

// q2,0,q2,0,R-q2,1,q2,1,R-q2,#,q4,#,R
e2, 0
e2, 0, >
e2, 1
e2, 1, >
e2, #
e4, #, >

// q3,x,q3,x,R-q3,0,q5,x,L
e3, x
e3, x, >
e3, 0
e5, x, <

// q4,x,q4,x,R-q4,1,q5,x,L
e4, x
e4, x, >
e4, 1
e5, x, <

// q5,0,q5,0,L-q5,1,q5,1,L-q5,x,q5,x,L-q5,#,q6,#,L
e5, 0
e5, 0, <
e5, 1
e5, 1, <
e5, x
e5, x, <
e5, #
e6, #, <

// q6,0,q6,0,L-q6,1,q6,1,L-q6,x,q0,x,R
e6, 0
e6, 0, <
e6, 1
e6, 1, <
e6, x
e0, x, >

// q7,x,q7,x,R-q7,_,q8,_,R
e7, x
e7, x, >
e7, _
e8, _, >
////////////////////////////////////////////////////////////////////////////////

